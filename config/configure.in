dnl Process this file with autoconf to produce a configure script.
dnl
dnl Copyright (c) 1997, by Carlo Wood <carlo@runaway.xs4all.nl>

dnl Make sure we are in the correct directory (someone could have run
dnl 'configure' with a wrong '--srcdir'); Note that a bug in autoconf
dnl forces us to have srcdir == dir where configure resides (config):
AC_INIT(config-sh.in)

dnl 'configure' must be run from within 'config/'.
AC_CONFIG_AUX_DIR(.)

dnl Define the input and output configuration header file,
dnl (Generate config/setup.h from config/setup.h.in):
AC_CONFIG_HEADER(setup.h)

dnl Demand at least version 2.13 of autoconf
AC_PREREQ(2.13)

dnl This should be done early.
AC_PROG_CC

dnl UNIX Variants
dnl Allow the use of BSD functions on AIX.
AC_AIX
dnl Allow the use of POSIX functions on several OS.
AC_ISC_POSIX
AC_MINIX
dnl ANSIfy the C compiler whenever possible.
AM_PROG_CC_STDC
dnl Use -O3 instead of -O2.
if test "$CFLAGS" != "" ; then
  CFLAGS=`echo "$CFLAGS" | sed -e 's/-O2/-O3/'`
fi
dnl Remove -pipe during configure
if test "$CFLAGS" != "" ; then
  CFLAGS=`echo "$CFLAGS" | sed -e 's/-pipe//g'`
fi

dnl Locate the library containing crypt
AC_SEARCH_LIBS(crypt, descrypt crypt, ,[AC_MSG_ERROR([Unable to find library containing crypt()])])

dnl Do all the checks necessary to figure out -lnsl / -lsocket stuff
AC_LIBRARY_NET

dnl Look for res_mkquery.  Done after AC_LIBRARY_NET in case res_mkquery
dnl is in one of those libraries somewhere.
AC_SEARCH_LIBS(__res_mkquery, resolv, ,[AC_SEARCH_LIBS(res_mkquery, resolv, ,[AC_MSG_ERROR([Unable to find library containing res_mkquery()])])])

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(malloc.h sys/malloc.h fcntl.h string.h strings.h sys/file.h sys/ioctl.h sys/time.h syslog.h unistd.h memory.h errno.h net/errno.h sys/cdefs.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_BIGENDIAN
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM
AC_TYPE_UID_T
unet_CHECK_TYPE_SIZES

dnl Define SIZE_T_FMT and TIME_T_FMT to be the printf format for
dnl respectively size_t and time_t.
unet_DEFINE_SIZE_T_FMT
unet_DEFINE_TIME_T_FMT

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_FUNC_MEMCMP
AC_FUNC_SETVBUF_REVERSED
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(strchr memcpy memmove)
AC_CHECK_FUNCS(gethostname gettimeofday mkdir strerror strtoken)
AC_CHECK_FUNCS(select socket uname)
AC_CHECK_FUNCS(setrlimit inet_netof getrusage times res_init)


dnl From licq: Copyright (c) 2000 Dirk Mueller
dnl Check if the type socklen_t is defined anywhere
AC_DEFUN(AC_C_SOCKLEN_T,
[AC_CACHE_CHECK(for socklen_t, ac_cv_c_socklen_t,
[
  AC_TRY_COMPILE([
    #include <sys/types.h>
    #include <sys/socket.h>
  ],[
    socklen_t foo;
  ],[
    ac_cv_c_socklen_t=yes
  ],[
    ac_cv_c_socklen_t=no
  ])
])
if test $ac_cv_c_socklen_t = no; then
  AC_DEFINE(socklen_t, int, [define to int if socklen_t not available])
fi
])

AC_C_SOCKLEN_T

dnl Comprueba si el tipo uintptr_t esta definido
AC_DEFUN(AC_C_UINTPTR_T,
[AC_CACHE_CHECK(for uintptr_t, ac_cv_c_uintptr_t,
[
  AC_TRY_COMPILE([
    #include <stdint.h>
  ],[
    uintptr_t foo;
  ],[
    ac_cv_c_uintptr_t=yes
  ],[
    ac_cv_c_uintptr_t=no
  ])
])
if test $ac_cv_c_uintptr_t = no; then
  AC_DEFINE(uintptr_t, unsigned long, [define to unsigned long if uintptr_t not available])
fi
])

AC_C_UINTPTR_T


#dnl comprueba el IPv6 de la maquina

AC_CHECK_TYPE([struct sockaddr_in6],
          [unet_have_sockaddr_in6=yes],
          [unet_have_sockaddr_in6=no],
[#include <sys/types.h>
#include <netinet/in.h>])

if test x"$unet_have_sockaddr_in6" = xyes; then
    AC_DEFINE([IPV6], 1, [Enable IPv6 support])
fi

#dnl Do we have a system call poll?
#unet_FUNC_POLL_SYSCALL

#dnl Do we have restarting syscalls ?
#AC_SYS_RESTARTABLE_SYSCALLS

dnl Test for programs
AC_PROG_AWK
AC_PROG_MAKE_SET
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PATH_PROGS(RMPROG, rm, /bin/rm)
AC_PATH_PROGS(SHPROG, sh, /bin/sh)

dnl Test if /bin/sh supports 'set -h'
AC_CACHE_CHECK([for set -h], unet_cv_sys_set_h,
[echo "set -h; exit $?" > conftest ;
$SHPROG ./conftest 2> conftest.out
ac_err=`grep -v '^ *+' conftest.out`
if test -z "$ac_err"; then
  unet_cv_sys_set_h=yes
else
  unet_cv_sys_set_h=no
fi
$RMPROG -fr conftest*])
dnl Used in configuretool.
AC_SUBST(unet_cv_sys_set_h)

unet_NONBLOCKING
unet_SIGNALS

dnl Add -pipe when possible
unet_PIPE_CFLAGS

dnl Used in config-sh.
AC_SUBST(ac_cv_header_poll_h)
AC_SUBST(ac_cv_header_syslog_h)
AC_SUBST(unet_cv_func_poll_syscall)

dnl Finally really generate all output files:
AC_OUTPUT(config-sh configuretool ../Makefile ../ircd/Makefile ../doc/Makefile Makefile, [echo timestamp > stamp-h;],)
